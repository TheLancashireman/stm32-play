/* Memory layout:
 *
 * The Blue Pill board (STM32F103C8T6) has 128 KiB flash at 0x08000000 and 20 KiB ram at 0x20000000
 *
 * The boot vector consists of:
 *		- the initial stack pointer
 *		- the starting address
 *
 * In order to boot correctly from flash, the boot vector must be at 0x08000000. This when the board is
 * configured to boot from flash, this block of memory gets aliased to 0 (the reset value of the vector base
 * address register) so it makes sense to put the whole vector table at 0x08000000.
*/

MEMORY
{
    flash	(rxa)	: org = 0x08000000, len = 0x20000	/* 128 KiB */
	ram		(rw)	: org = 0x20000000, len = 0x5000	/* 20 KiB */
}

SECTIONS
{
    .text	0x08000000	:
	{
		*(.vectors*)
		*(.text*)
	} > flash

	.rodata	BLOCK(16) :
	{
		*(.rodata*)
	} > flash

	.idata	BLOCK(16) :
	{
		dv_idata = .;
		KEEP(*(.dv_dummysection))
	} > flash

	.data	0x20000000	: AT( ADDR(.idata) )
	{
		dv_start_data = .;
		*(.data*)
		FILL(0x00)
		. = ALIGN(16);
		dv_end_data = .;
	} > ram

    .bss	BLOCK(16) (NOLOAD) :
	{
		dv_start_bss = .;
		*(.bss*)
		dv_end_bss = .;
	} > ram

dv_stacktop = (0x20000000 + 0x5000);

  /* These sections appear to be generated by the compiler.
   * We doesn't use them.
  */
    .stack              : { *(.stack)       }

  /* The remainder are DWARF-2 debug sections. They contain
   * ELF relocations and must be located at zero.
  */
    . = 0x0;
    .debug_aranges      : { *(.debug_aranges)   }
    . = 0x0;
    .debug_pubnames     : { *(.debug_pubnames)  }
    . = 0x0;
    .debug_info         : { *(.debug_info)  }
    . = 0x0;
    .debug_abbrev       : { *(.debug_abbrev)    }
    . = 0x0;
    .debug_line         : { *(.debug_line)  }
}
